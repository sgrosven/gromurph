apply plugin: 'java'

/*
Usage update, March 2016
  - use Gradle for basic compiling, testing, dependency management
  - use Ant for Izpack release processing (AND therefore 'assemble')
  - both Gradle and Ant use Gradle-like directory style
*/

import org.apache.tools.ant.filters.ReplaceTokens

/* UPDATED THESE FOR EACH RELEASE!!! */
version = '7.2016.1'
ext.releaseDate = '31 March 2016'
sourceCompatibility = 1.7

ext.buildDir = "build"
ext.distDir = "build/distributions"
ext.assembleDir = "build/assemble"
ext.libsDir = "build/libs"

jar {
    archiveName = "JavaScore.jar"
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.gromurph.javascore.JavaScore'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

repositories {
    maven {
    	url './local-repo'
    }
    mavenCentral()
    jcenter()
}

dependencies {
   compile 'commons-collections:commons-collections:3.2'
   compile 'javax.xml.bind:jaxb-api:2.2.12'
   compile 'javax.help:javahelp:2.0.05'
   compile 'com.toedter:jcalendar:1.4'
   compile 'org.slf4j:slf4j-api:1.7.10'
   compile 'org.apache.logging.log4j:log4j-api:2.1'
   compile 'org.apache.logging.log4j:log4j-core:2.1'
   compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.1'
   compile 'commons-io:commons-io:2.4'
   compile 'commons-net:commons-net:3.3'
   compile 'commons-codec:commons-codec:1.10'
   compile 'jakarta-regexp:jakarta-regexp:1.4'
   compile 'org.apache.httpcomponents:httpclient:4.4'
    
   // testing only 
   testCompile 'junit:junit:4.11'
   testCompile 'org-uispec4j:uispec4j:2.5-SNAPSHOT'
   
   // local repo
   compile 'dbswing:dbswing:0.0'
   compile 'sailing-org:sailingXrr:1.3'
    
}

task initializeJavaScoreVersion << {
	println "compileJava.doFirst, version: $version, releaseDate: $releaseDate"
	
	file('src/main/java/org/gromurph/javascore/JavaScoreVersion.java').delete()
	copy {
		from 'src/main/izpack'
		into 'src/main/java/org/gromurph/javascore'
		include 'master.JavaScoreVersion.java'
		filter( ReplaceTokens, tokens: [ version: version, releasedate: releaseDate])
		rename 'master.JavaScoreVersion.java', 	'JavaScoreVersion.java'
	}
}

compileJava.dependsOn 'initializeJavaScoreVersion'

